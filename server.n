using System;
using System.Collections.Generic;
using System.Collections.Specialized;
using System.IO;
using System.Net;
using System.Text.Encoding;
using System.Text.RegularExpressions;

using Nemerle.IO;
using Nemerle.Utility;


namespace httplib {

//------------------------------------------------------------------------------
// HTTP (method)

// Defined in order used in http://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html
public enum HTTP {
    | INVALID_

    | OPTIONS
    | GET
    | HEAD
    | POST
    | PUT
    | DELETE
    | TRACE
    | CONNECT

    | COUNT_
}


//------------------------------------------------------------------------------
// Array

public class Array {

    type SystemType = array[string];

    arr: SystemType;

    public this(data: SystemType) {
        arr = data;
    }

    public Item[index: int]: string {
        get { arr[index]; }
    }

    public Length: int {
        get { arr.Length; }
    }

    public forEach(lambda: string -> void): void {
        foreach(v in arr) {
            lambda(v);
        }
    }

    public toSystemType(): SystemType {
        arr;
    }
}


//------------------------------------------------------------------------------
// HashTable

public class HashTable {

    type SystemType = NameValueCollection;

    nvc: SystemType;

    public this() {
        nvc = SystemType();
    }

    public this(data: SystemType) {
        nvc = data;
    }

    public Item[key: string]: string {
        get { nvc[key]; }
        set { nvc[key] = value; }
    }

    public Keys: array[string] {
        get { nvc.AllKeys; }
    }

    public forEach(lambda: string * string -> void): void {
        foreach(k in Keys) {
            lambda(k, Item[k]);
        }
    }

    public toSystemType(): SystemType {
        nvc;
    }
}


//------------------------------------------------------------------------------
// Request

public class Request {

    [Accessor] http_method: string;
    [Accessor] url: Uri;
    [Accessor] raw_url: string;
    [Accessor] query_string: HashTable;
    [Accessor] headers: HashTable;
    [Accessor] path: Array;
    [Accessor] accept: Array;
    [Accessor] raw_path: string;
    [Accessor] extension: string;
    [Accessor] is_authenticated: bool;
    [Accessor] is_secure: bool;
    [Accessor] is_web_socket: bool;
    [Accessor] http_version: int;
    // TODO: is it possible to refactor these setters/mutables away?
    [Accessor(flags = WantSetter)] mutable vars: HashTable;
    [Accessor(flags = WantSetter)] mutable file_stream: FileStream;

    public this(req: HttpListenerRequest) {

        http_method = req.HttpMethod;
        url = req.Url;
        raw_url = req.RawUrl;
        query_string = HashTable(req.QueryString);
        headers = HashTable(req.Headers);
        path = makePathArray(RawUrl);
        accept = Array(req.AcceptTypes);
        raw_path = makeRawPath(Path);
        extension = makeExtension(Path);
        is_authenticated = req.IsAuthenticated;
        is_secure = req.IsSecureConnection;
        is_web_socket = req.IsWebSocketRequest;
        http_version = makeHttpVersion(req.ProtocolVersion);
        vars = HashTable();
        file_stream = null;
    }

    /** convert a RawUrl -> path component Array
     *  "/a/b//c" -> [ "a", "b", "c" ] */
    makePathArray(rawUrl: string): Array {

        def path_only = rawUrl.Split(array['?'], StringSplitOptions.None);
        def path_section =
            if(path_only.Length > 0) {
                path_only[0]
            } else {
                rawUrl
            };
        def path_split = path_section.Split(
            array['/'], StringSplitOptions.RemoveEmptyEntries );

        Array(path_split);
    }

    makeExtension(path: Array, full: bool = false): string {

        def last_path =
            if(path.Length > 0) {
                path[path.Length - 1];
            } else {
                ""
            }
        def last_split = last_path.Split(
            array['.'], StringSplitOptions.None );

        if(last_split.Length > 1) {
            if(full) {
                def len = last_split.Length - 1;
                mutable tmp: array[string] = array(len);
                Array.Copy(last_split, 1, tmp, 0, len);
                String.Join(".", tmp)
            } else {
                last_split[last_split.Length - 1];
            }
        } else {
            ""
        }
    }

    makeRawPath(path: Array): string {
        String.Join("/", path.toSystemType());
    }

    makeHttpVersion(version: Version): int {
        (version.Major) + 10 + (version.Minor);
    }
}


//------------------------------------------------------------------------------
// Response

public class Response {

    public ContentLength: long {
        get { response.ContentLength64 }
        set { response.ContentLength64 = value; }
    }

    public ContentType: string {
        get { response.ContentType }
        set { response.ContentType = value; }
    }


    public StatusCode: int {
        get { response.StatusCode }
        set { response.StatusCode = value; }
    }

    public StatusDescription: string {
        get { response.StatusDescription }
        set { response.StatusDescription = value; }
    }

    mutable response: HttpListenerResponse;
    mutable output: array[byte];

    public this(res: HttpListenerResponse, output_length = 1024) {

        response = res;
        output = array(output_length);
    }

    public append(params data: array[string]): void {
        appendArray(data);
    }

    public appendArray(data: array[string]): void {

        foreach(d in data) {
            def buffer = UTF8.GetBytes(d);
            writeToOutput(buffer);
        }
    }

    public appendBytes(data: array[byte]): void {
        writeToOutput(data);
    }

    writeToOutput(data: array[byte]): void {
        def new_length = ContentLength + data.Length;

        when(output.Length < new_length) {
            def _32 = output.Length * 3 / 2;
            def nl = if(_32 > new_length) { _32 } else { new_length :> int }
            Array.Resize(ref output, nl);
        }

        data.CopyTo(output, ContentLength);
        ContentLength = new_length;
    }

    internal complete(): void {
        def os = response.OutputStream;
        os.Write(output, 0, ContentLength :> int);
        os.Close();
    }
}


//------------------------------------------------------------------------------
// ServerConfiguration

[Record]
public class ServerConfiguration {

    [Accessor(flags = WantSetter)] mutable prefixes: array[string];
    [Accessor(flags = WantSetter)] mutable output_length: int;
    [Accessor(flags = WantSetter)] mutable root: string;
}



//------------------------------------------------------------------------------
// Route

public class Route {

    [Accessor] pattern: string;
    regex: Regex;
    mutable captures: array[string] = array(0);

    public this(path: string) {

        mutable rx = path;

        // {{name}}  -->  ([A-Za-z0-9.-]+)
        process(ref rx, @"{{(\w+)}}", _ => { @"([A-Za-z0-9_.-]+)" });

        // {{name:pattern}}  -->  (pattern)
        process(ref rx, @"{{(\w+):(.+)}}", p => { "(" + p + ")" });

        //anchor start/end of regex
        rx = "^" + rx + "$";

        regex = Regex(rx);
        pattern = rx;
    }

    public isMatch(path: string): bool {
        regex.IsMatch(path)
    }

    public doMatch(path: string): bool * HashTable {
        mutable vars = HashTable();
        def result = regex.IsMatch(path);

        when(result) {
            def _ = regex.Replace(path, m => {

                for(mutable i = 1; i < m.Groups.Count; i++) {
                    def k = captures[i - 1];
                    def v = m.Groups[i].ToString();
                    vars[k] = v;
                }
                ""
            }, 100, 0);
        }
        (result, vars);
    }

    process(input: ref string, pattern: string, replace: string -> string): void {
        input = Regex.Replace(input, pattern, m => {

            def v: string = m.Groups[1].ToString();
            def patt: string = m.Groups[2].ToString();
            def e = captures.Length;

            Array.Resize(ref captures, e + 1);
            captures[e] = v;

            replace(patt);
        });
    }
}


//------------------------------------------------------------------------------
// Server

public abstract class Server: IDisposable {

    public type Callback = Request * Response -> void;

    type Method = HTTP;
    public static MethodCount = HTTP.COUNT_ :> int;

    [System.Flags]
    public enum State {
        | Neutral = 0x00
        | Running = 0x01
        | Closed = 0x02
    }

    [Accessor(Flags, flags = Protected)]
    [FlagAccessor(Running, flags = Protected | WantSetter)]
    [FlagAccessor(Closed, flags = Protected | WantSetter)]
    mutable state: State = State.Neutral;

    [Accessor(flags = Protected)]
    listener: HttpListener = HttpListener();

    [Accessor(flags = Protected)]
    configuration: ServerConfiguration;

    public Routes[i: Method]: Dictionary[string, Route * Callback] {
        get { routes[i :> int] }
    }
    routes: array[Dictionary[string, Route * Callback]] = array(MethodCount);

    public this(config: ServerConfiguration) {
        configuration = config;

        foreach(p in Configuration.Prefixes) {
            print("Adding \"$p\"...\n");
            Listener.Prefixes.Add(p);
        }
        for(mutable i = 0; i < MethodCount; i++) {
            routes[i] = Dictionary();
        }

        // install default handlers for several methods...
        route(HTTP.INVALID_, ".*", internalServerErrorResponse);
        route(HTTP.OPTIONS, ".*", methodNotAllowedResponse);
        route(HTTP.TRACE, ".*", methodNotAllowedResponse);
        route(HTTP.CONNECT, ".*", methodNotAllowedResponse);

        Listener.Start();
    }

    public Dispose(): void {
        shutdown();
    }

    protected override Finalize(): void {
        shutdown();
    }

    shutdown(): void {

        when(!Closed) {
            Listener.Close();
            Running = false;
            Closed = true;
        }
    }

    // route("/a/b/c", r => { })
    public route(method: Method, path: string, fn: Callback): void {
        def r = Route(path);
        def s = r.Pattern;
        print("route: $(methodName(method)) '$(path)' added $(fn)\n");
        Routes[method].Add(s, (r, fn));
    }

    public update(): void {
        Running = true;
        def _ = Listener.BeginGetContext(
            AsyncCallback(listenerCallback), this);
    }

    findCallback(method_name: string, path: string, vars_out: ref HashTable): Callback {
        mutable result;
        exit: {
            def method = methodType(method_name);
            foreach(r in Routes[method]) {
                def (route, fn) = r.Value;
                def (ok, vars) = route.doMatch(path);

                when(ok) {
                    print("'$(path)' matches '$(route.Pattern)'...\n");
                    result = fn;
                    vars_out = vars;
                    exit();
                }
            }
        }
        result;
    }

    /** Overridden by child classes */
    protected abstract callback(request: Request, response: Response): void;

    handleCallback(context: HttpListenerContext): void {
        mutable request, response;
        try {
            request = Request(context.Request);
            response = Response(context.Response, Configuration.OutputLength);

            def path = Path.Combine(Configuration.Root, request.RawPath);
            def exists = File.Exists(path);

            when(exists) {
                def file_stream = FileStream(path, FileMode.Open);
                request.FileStream = file_stream;
            }

            print("Requesting: \"$(request.RawPath)\"...\n");

            // find callback (and populate vars from route vars if any)
            mutable vars = HashTable();
            def cb = findCallback(request.HttpMethod, "/" + request.RawPath, ref vars);

            // populate vars from body of POST...
            when(HTTP.POST == methodType(request.HttpMethod)) {
                processInputStream(context.Request, ref vars);
            }

            if(cb != null) {
                request.Vars = vars;
                cb(request, response);
            } else {
                callback(request, response);
            }
        } catch {
            | _ => internalServerErrorResponse(request, response);
        } finally {
            response.complete();
        }
    }

    static listenerCallback(result: IAsyncResult): void {
        def server = result.AsyncState :> Server;
        def context = server.Listener.EndGetContext(result);

        server.handleCallback(context);
    }

    processInputStream(request: HttpListenerRequest, vars_out: ref HashTable): void {

        when(request.InputStream != null) {
            using(mutable reader = StreamReader(
                request.InputStream, request.ContentEncoding)) {

                def str = reader.ReadToEnd();

                def each_var = str.Split(array['&', ';'], StringSplitOptions.None);

                when(each_var.Length > 0) {

                    foreach(v in each_var) {
                        def s = v.Split(array['='], StringSplitOptions.None);

                        when(s.Length > 1) {
                            vars_out[s[0].ToString()] = s[1].ToString();
                        }
                    }
                }
            }
        }
    }

    static methodType(name: string): Method {
        match(name.ToUpper()) {
            | "OPTIONS" => HTTP.OPTIONS
            | "GET" => HTTP.GET
            | "HEAD" => HTTP.HEAD
            | "POST" => HTTP.POST
            | "PUT" => HTTP.PUT
            | "DELETE" => HTTP.DELETE
            | "TRACE" => HTTP.TRACE
            | "CONNECT" => HTTP.CONNECT
            | _ => HTTP.INVALID_
        }
    }

    static methodName(mth: Method): string  {
        match(mth) {
            | HTTP.OPTIONS => "OPTIONS"
            | HTTP.GET => "GET"
            | HTTP.HEAD => "HEAD"
            | HTTP.POST => "POST"
            | HTTP.PUT => "PUT"
            | HTTP.DELETE => "DELETE"
            | HTTP.TRACE => "TRACE"
            | HTTP.CONNECT => "CONNECT"
            | _ => "?"
        }
    }

    // stock request handlers...

    internalServerErrorResponse(_req: Request, res: Response): void {
        res.StatusCode = 500;
        res.StatusDescription = "Internal Server Error"
    }

    methodNotAllowedResponse(_req: Request, res: Response): void {
        res.StatusCode = 405;
        res.StatusDescription = "Method Not Allowed"
    }
}


} // namespace httplib
