using System;

using Nemerle.Extensions;
using Nemerle.IO;

using httplib;


namespace forum {

class ForumServer: Server {

    public this(path: string) {
        base(path);

        route(HTTP.GET, "/", indexResponse);
        route([
            (HTTP.GET, "/th/{{id:[0-9]+}}/{{rest:.*}}"),
            (HTTP.GET, "/th/{{id:[0-9]+}}")], testResponse);
        route(HTTP.GET, "/nav-{{action}}", navResponse);
        route(HTTP.POST, "/nav-signin", navSigninResponse);
        route(HTTP.GET, "/pwtest", pwResponse);
    }

    public indexResponse(
        _context: Context, request: Request, response: Response): void {

        response.append(renderPage("index.nyml"));
        response.Status = (200, "OK");
    }

    public navResponse(
        context: Context, request: Request, response: Response): void {

        def action = context.Vars["action"];
        def lambda = match(action) {
            | "login" => navLoginResponse
            | "ok" => navOkResponse
            | _ => methodNotAllowedResponse
        };
        lambda(context, request, response);
    }

    public navLoginResponse(
        context: Context, request: Request, response: Response): void {

        if(request.IsSecure) {

            def cookies = request.Cookies;
            def auth = if(cookies != null) { cookies["_auth"] } else { null };
            if(!String.IsNullOrEmpty(auth)) {
                navOkResponse(context, request, response);
            } else {
                response.append(renderPage("nav-login.nyml"));
                response.Status = (200, "OK");
            }
        } else {
            response.append("<html><body><h1>Forbidden!</h1></body></html>");
            response.Status = (403, "Forbidden");
        }
    }

    public navSigninResponse(
        context: Context, request: Request, response: Response): void {

        def user = context.Vars["user"];
        def password = context.Vars["password"];
        def sec = if(request.IsSecure) { "Security OK" } else { "INSECURE OH NOES!?" };

        response.append(
            "<html><body><h1>Signin attempt</h1>"
            $"<p>user = $user</p><p>password = $password</p>"
            "</body></html>");

        if(request.IsSecure) {
            // System.Net.Cookie flags don't seem to be respected, so set a
            // cookie header explicitly...
            response["Set-Cookie"] = "_auth=1; path=/; secure; httponly";
            response["Location"] = "http://localhost:8080/";
            response.Status = (303, "See Other");
        } else {
            response.append("<html><body><h1>Forbidden!</h1></body></html>");
            response.Status = (403, "Forbidden");
        }
    }

    public navOkResponse(
        context: Context, request: Request, response: Response): void {

        if(request.IsSecure) {
            response.append("<html><body><h1>Login Ok!</h1></body></html>");
            response.Status = (200, "OK");
        } else {
            response.append("<html><body><h1>Forbidden!</h1></body></html>");
            response.Status = (403, "Forbidden");
        }
    }

    public testResponse(
        context: Context, _request: Request, response: Response): void {

        def id = context.Vars["id"];
        def rest = context.Vars["rest"];

        response.append(
            $"<html><body><h1>Thread ID=$id</h1>"
            $"<p><tt>rest=$rest</tt></p></body></html>");
        response.Status = (200, "OK");
    }

    public pwResponse(
        _context: Context, _request: Request, response: Response): void {

        def auth = getModuleInterface.[mod.IAuth]();
        if(auth != null) {
            def test_pw = "n0ts3cr3t";
            def record = auth.createPasswordRecord(test_pw);
            mutable sw = Diagnostics.Stopwatch();
            sw.Start();
            def valid = auth.validatePassword(test_pw, record);
            sw.Stop();
            def tvalid = sw.ElapsedMilliseconds;
            sw.Reset();
            sw.Start();
            def invalid = auth.validatePassword("owls", record);
            sw.Stop();
            def tinvalid = sw.ElapsedMilliseconds;
            sw.Reset();
            response.append("<html><body><h1>Auth test</h1>"
                $"<p>Input password is: '$test_pw'.</p>"
                $"<p>Password record is: '$record'.</p>"
                $"<p>Password record length is: '$(record.Length)'.</p>"
                $"<p>Validation result is: '$valid'.</p>"
                $"<p>Validation took: $(tvalid)ms.</p>"
                $"<p>Failed result is: '$invalid'.</p>"
                $"<p>Failed took: $(tinvalid)ms.</p>"
                "</body></html>");
            response.Status = (200, "OK");
        } else {
            response.append("<html><body><h1>Error: no auth module</h1></body></html>");
            response.Status = (500, "Err");
        }
    }

    protected override internalServerError(ex: Exception): void {
        print("INTERNAL SERVER ERROR!\n"
            $"$(ex.Message)\n"
            $"$(ex.StackTrace)\n");
    }
}


} // namespace forum
