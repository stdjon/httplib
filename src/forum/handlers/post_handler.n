using System;

using Nemerle.Extensions;

using httplib;


namespace forum {

[HasHandlerFactory]
public class PostHandler: BaseHandler {

    public this(hd: HandlerData, path: string) {
        base(hd, path);
    }

    public override initialize(): void {
        base.initialize();

        def pid = Int32.Parse(Context.Vars["num"]);
        def command = Server.Database.createCommand(
            "SELECT UserId, ThreadId, CreateDate, Points, OutputContent FROM "
                "Posts WHERE Id = @Id") <- [ "@Id" = pid ];

        mutable uid;
        mutable thid;
        mutable date;
        mutable points;
        mutable content;
        mutable ok = false;

        using(reader = command.executeReader()) {
            reader.enumerate(() => {
                uid = reader.getValue.[uint?]("UserId", 0);
                thid = reader.getValue.[uint?]("ThreadId", 0);
                date = reader.getValue.[long]("CreateDate", 0);
                points = reader.getValue.[int?]("Points", 0);
                content = reader.getValue("OutputContent");
                ok = true;
            });
        }

        when(ok) {
            def command2 = Server.Database.createCommand(
                "SELECT Name from Users WHERE Id = @Id") <- [ "@Id" = uid ];
            def name = command2.executeScalar() :> string;

            def command3 = Server.Database.createCommand(
                "SELECT Title, Colour from Threads WHERE Id = @Id") <- [ "@Id" = thid ];

            mutable title, colour;
            using(reader = command3.executeReader()) {
                reader.enumerate(() => {
                    title = reader.getValue("Title");
                    colour = reader.getValue("Colour");
                });
            }

            _ = PageData <- [
                "PostId" = pid,
                "User" = name,
                "Thread" = title,
                "ThreadId" = thid,
                "CreateDate" = (date > 0) ? Date.FromTicks(date).ToString() : "never",
                "Points" = points,
                "Content" = content,
                "ColourId" = colour,
            ];
        }
    }
}


} // forum
