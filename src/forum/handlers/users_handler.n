using System;
using System.Text.RegularExpressions;

using Nemerle.Extensions;

using httplib;


namespace forum {

[HasHandlerFactory]
public class UsersHandler: BasePageHandler {

    public UsersPerPage: int { get; }
    public IsPlural: bool { get; }
    public Transform: string { get; private set; }
    public Font: string { get; private set; }

    public this(hd: HandlerData, path: string, plural: bool) {
        base(hd, path);
        IsPlural = plural;
        UsersPerPage = Server.Configuration.Limits.UsersPerPage;
    }


    public override initialize(): void {
        base.initialize();
        if(IsPlural) {
            initializeUsers();
        } else {
            initializeSingle();
        }
    }

    public initializeSingle(): void {
        mutable page_userid;

        def session_userid = ForumDatabase.
            getUserIdFromCookie(Request, STOK.INSECURE);
        def user = Context.Vars["user"];
        def command = Database.createCommand(
            "SELECT Id FROM Users WHERE Name = @Name") <- [ "@Name" = user ];
        page_userid = command.executeScalar() :> uint?;

        def is_prefs =
            (session_userid != 0) && (page_userid != null) &&
            (session_userid == page_userid);

            if(is_prefs) {
                initializePrefs(session_userid, user);
            } else {
                initializeUser(user);
            }

        PageData["IsUserPrefs"] = is_prefs ? "true" : "false";
    }

    public initializeUsers(): void {

        def fromstr = Context.Vars["from"];
        def from = fromstr ? Int32.Parse(fromstr) : 1;
        def tostr = Context.Vars["to"];
        def to = tostr ? Int32.Parse(tostr) : UsersPerPage;
        def dist = to + 1 - from;
        mutable users: PageDataArray = array(dist);
        mutable i = 0;

        def command = Database.createCommand(
            "SELECT Name, Level, Points, Motto, Colour FROM Users "
                "ORDER BY Name LIMIT @From, @To") <- [
                "@From" = from - 1,
                "@To" = to - 1,
            ];

        i = command.execute((j, reader) => {
            when(j < dist) {
                def u = reader.getValue("Name");
                when(u != null) {
                    def points = reader.getValue("Points", 0);
                    def level = reader.getValue("Level", 0.0f);
                    def motto = reader.getValue("Motto", "");
                    def colour = reader.getValue("Colour",
                        Server.Configuration.DefaultColourClass);
                    users[j] = PageData() <- [
                        "Num" = (from + j).ToString(),
                        "Name" = u,
                        "Points" = points.ToString(),
                        "Level" = level.ToString("f2"),
                        "Motto" = motto,
                        "ColourBgHsl" = CssHandler.bgFromColourId(colour),
                        "ColourFgHsl" = CssHandler.fgFromColourId(colour),
                    ];
                }
            }
        });

        when(i < dist) {
            Array.Resize(ref users, i);
        }

        def prf = Server.getDefaultPrefix(Request);
        mutable prev = null;
        mutable next = null;

        Log.d("from = {0} > 1 {1}", from, from > 1);
        when(from > 1) {
            def pf = Math.Max(1, from - dist);
            def pt = pf + dist - 1;
            prev = $"$prf/u/$pf-$pt";
        }
        Log.d("prev = {0}", prev);

        def user_count = ForumDatabase.getUserCount();
        Log.d("to = {0} < {1} {2}", to, user_count, to < user_count);
        when(to < user_count) {
            def nf = from + dist;
            def nt = nf + dist - 1;
            next = $"$prf/u/$nf-$nt";
        }
        Log.d("next = {0}", next);

        // Add a marker for every fifth post (1, 6, 11...)
        def marker_count = (i + 4) / 5;
        def markers: PageDataArray = array(marker_count);
        for(mutable j = 0; j < marker_count; j++) {
            markers[j] = PageData() <- [ "Mark" = (j * 5) + from ];
        }

        _ = PageData <- [
            "Users" = users,
            "Prev" = prev,
            "Next" = next,
            "Markers" = markers,
        ];
    }

    public initializeUserCommon(user: string): void {

        def command = Database.createCommand(
            "SELECT Name, Motto, Location, JoinDate, Points, Level, Colour, "
                "Transform, Font FROM Users WHERE Name = @Name") <- [
            "@Name" = user
        ];

        command.execute(reader => {
            def date: long = reader.getValue("JoinDate", 0L);
            Transform = reader.getValue("Transform",
                Server.Configuration.DefaultTransform);
            Font = reader.getValue("Font",
                Server.Configuration.DefaultFontClass);

            _ = PageData <- [
                "User" = reader.getValue("Name", user),
                "Motto" = reader.getValue("Motto", ""),
                "Location" = reader.getValue("Location", ""),
                "JoinDate" = (date > 0) ? Date.FromTicks(date).ToString() : "never",
                "Points" = reader.getValue("Points", 0),
                "Level" = reader.getValue("Level", 0.0f).ToString("f2"),
                "ColourId" = reader.getValue("Colour",
                    Server.Configuration.DefaultColourClass),
                "Font" = Font,
            ];
        });
    }

    public initializeUser(user: string): void {

        initializeUserCommon(user);

        mutable threads = array(10);
        mutable posts = array(10);
        mutable i;

        def command = Database.createCommand(
            "SELECT Id FROM Users WHERE Name = @Name") <- [ "@Name" = user ];
        def userid_ = command.executeScalar() :> uint?;
        def userid = userid_ ? (userid_ :> uint) : 0;

        def command2 = Database.createCommand(
            "SELECT Id, Title, CreateDate FROM Threads WHERE "
                "UserId = @UserId ORDER BY CreateDate DESC LIMIT 0, 9") <- [
            "@UserId" = userid,
        ];
        i = command2.execute((j, reader) => {
            threads[j] = PageData() <- [
                "Id" = reader.getValue("Id", 0U),
                "Title" = reader.getValue("Title"),
                "CreateDate" = Date.FromTicks(reader.getValue("CreateDate", 0L)),
            ];
        });
        Array.Resize(ref threads, i);

        def command3 = Database.createCommand(
            "SELECT Id, OutputContent, CreateDate FROM Posts WHERE "
                "UserId = @UserId ORDER BY CreateDate DESC LIMIT 0, 9") <- [
            "@UserId" = userid,
        ];
        i = command3.execute((j, reader) => {
            mutable content = reader.getValue("OutputContent");
            when(content.Length > 100) {
                content = content.Substring(0, 100) + "&hellip;";
            }
            // Use HtmlSanitizer to close any tags lost by the truncation.
            def htmlsan = Server.
                getModuleInterface.[ITextTransform]("HtmlSanitizer");
            content = htmlsan.transform(content);

            // Strip out <hN>, <p> and <br> tags for the post preview
            // TODO: use a CsQuery-based module to allow proper tag replacement.
            content = Regex(@"</?h[0-9]+>").Replace(content, "");
            content = Regex(@"</?p>").Replace(content, "");
            content = content.Replace("<br>", "");

            posts[j] = PageData() <- [
                "Id" = reader.getValue("Id", 0U),
                "Content" = content,
                "CreateDate" = Date.FromTicks(reader.getValue("CreateDate", 0L)),
            ];
        });
        Array.Resize(ref posts, i);

        PageData["Threads"] = threads;
        PageData["Posts"] = posts;
    }

    public initializePrefs(userid :uint, user: string): void {

        initializeUser(user);

        match(Transform) {
            | "bbcode" => PageData["BbCodeActive"] = "active"
            | "textile" => PageData["TextileActive"] = "active"
            | "htmlsan" => PageData["HtmlSanActive"] = "active"
            | _ => {}
        }

        match(Font) {
            | "serif1" => PageData["Serif1Active"] = "active"
            | "serif2" => PageData["Serif2Active"] = "active"
            | "sans1" => PageData["Sans1Active"] = "active"
            | "sans2" => PageData["Sans2Active"] = "active"
            | _ => {}
        }

        _ = PageData <- [
            "Transform" = Transform,
            "IsPrefs" = true,
            "Id" = userid,
        ];
    }
}


} // forum
