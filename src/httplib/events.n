using System;
using System.Collections.Generic;


namespace httplib {

public delegate ServerEvent(j: JsonObject): void;


// TODO: This class seems somewhat unnecessary - is it possible to store a .NET
// event in a Dictionary directly?
class ServerEventHolder {

    public event OnEvent: ServerEvent;

    public call(j: JsonObject): void {
        OnEvent ? OnEvent(j) : ();
    }
}


[HasLogging]
public class ServerEventSource {

    Events: Dictionary[string, ServerEventHolder] { get; private set; }

    public this() {
        Events = Dictionary();
    }

    public sendEvent(channel: string, j: JsonObject): void {
        mutable probe;
        when(Events.TryGetValue(channel, out probe)) {
            probe.call(j);
        }
    }

    public addListener(channel: string, lst: ServerEventListener): void {
        when(!Events.ContainsKey(channel)) {
            Events[channel] = ServerEventHolder();
        }
        Events[channel].OnEvent += lst.onEvent;
    }

    public removeListener(channel: string, lst: ServerEventListener): void {
        Events[channel].OnEvent -= lst.onEvent;
    }
}


[HasLogging]
public class ServerEventListener: IDisposable {

    public Source: ServerEventSource { get; private set; }
    public Channel: string { get; private set; }
    public IsDisposed: bool { get; private set; }

    public this(src: ServerEventSource, channel: string) {
        Source = src;
        Channel = channel;
        IsDisposed = false;
        Source.addListener(Channel, this);
    }

    public Dispose(): void {
        when(!IsDisposed) {
            Source.removeListener(Channel, this);
            IsDisposed = true;
        }
    }

    public virtual onEvent(_j: JsonObject): void {
        // do stuff with _j...
    }
}


} // namespace httplib
