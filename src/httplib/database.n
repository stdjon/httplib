using System;
using System.Data;
using System.Reflection;

using Nemerle.Utility;


namespace httplib {

[HasLogging]
public abstract class Database: IDisposable  {

    [Accessor(flags = WantSetter)]
    protected mutable connection: IDbConnection = null;

    // This variable is nonzero iff a transaction has begun on the DB, and has
    // not yet committed or rolled back.
    mutable in_transaction: uint;

    // Lock objects
    execute_lock: object;
    transaction_lock: object;

    public virtual Initialized: bool {
        get { Connection != null }
    }

    public this(connection: string = null) {
        in_transaction = 0;
        execute_lock = Object();
        transaction_lock = Object();

        when(connection != null) {
            initialize(connection);
        }
        when(Connection != null) {
            open();
        }
    }

    public Dispose(): void {
        shutdown();
    }

    protected override Finalize(): void {
        shutdown();
    }

    shutdown(): void {
        close();
        Connection.Dispose();
    }

    public abstract initialize(connection: string): void;

    public virtual open(): void {
        Connection.Open();
    }

    public virtual close(): void {
        Connection.Close();
    }

    public withTransaction(
        lambda: Database -> void, handle: Exception -> void = null): void {

        lock(transaction_lock) {
            mutable transaction = null;
            try {
                when(0 == in_transaction) {
                    transaction = Connection.BeginTransaction();
                }

                in_transaction++;
                _ = lambda(this);
                in_transaction--;

                when(0 == in_transaction) {
                    transaction.Commit();
                }
            } catch {
                | ex => {
                    when(0 != in_transaction) {
                        Log.w("Rolling back...");
                        transaction.Rollback();
                        in_transaction = 0;
                    }
                    when(handle != null) {
                        handle(ex);
                    }
                }
            }
        }
    }

    public virtual createCommand(query: string = null): Command {
        mutable result = Command(Connection.CreateCommand(), execute_lock);
        when(query != null) {
            result.Query = query;
        }
        result;
    }

    /** Load a DB by reflection, given a DLL and type name. */
    public static load(
        path: string, name: string, param: string = null): Database {

        def assembly = Assembly.LoadFrom(path);
        def type = assembly.GetType(name);

        Activator.CreateInstance(type, param) :> Database;
    }
}


public class Command {

    [Accessor] protected command: IDbCommand;
    [Accessor] execute_lock: object;

    public virtual Query: string {
        get { Command.CommandText; }
        set { Command.CommandText = value; }
    }

    public virtual Parameters: IDataParameterCollection {
        get { Command.Parameters; }
    }

    public this(c: IDbCommand, l: object) {
        command = c;
        execute_lock = l;
    }

    public virtual createParameter(
        name: string = null, value: object = null): Parameter {

        mutable result = Parameter(Command.CreateParameter());

        when(name != null) {
            result.Name = name;
        }
        when(value != null) {
            result.Value = value;
        }

        result;
    }

    /** For use with Nemerle.Extensions <- operator...
     *  db.createCommand("...@X...") <- [ "@X" = x, ... ]; */
    public virtual Add[T](name: string, value: T): void {
        addParameter(name, value);
    }

    public virtual addParameter(param: Parameter): void {
        _ = Parameters.Add(param.Parameter);
    }

    public virtual addParameter(name: string, value: object): void {
        addParameter(createParameter(name, value));
    }

    public virtual executeNonQuery(): int {
        lock(ExecuteLock) {
            Command.ExecuteNonQuery();
        }
    }

    public virtual executeReader(): DataReader {
        lock(ExecuteLock) {
            DataReader(Command.ExecuteReader());
        }
    }

    public virtual execute(lambda: DataReader -> void): void {
        lock(ExecuteLock) {
            using(reader = executeReader()) {
                reader.enumerate(() => {
                    lambda(reader);
                })
            }
        }
    }

    public virtual execute(lambda: int * DataReader -> void): int {
        mutable result;
        lock(ExecuteLock) {
            using(reader = executeReader()) {
                result = reader.enumerate(i => {
                    lambda(i, reader);
                })
            }
        }
        result;
    }

    public virtual executeScalar(): object {
        lock(ExecuteLock) {
            // Convert DBNull values to plain ol' null...
            def v = Command.ExecuteScalar();
            v is DBNull ? null : v;
        }
    }
}


public class Parameter {

    [Accessor] protected parameter: IDbDataParameter;

    public virtual Name: string {
        get { Parameter.ParameterName; }
        set { Parameter.ParameterName = value; }
    }

    public virtual Value: object {
        get { Parameter.Value; }
        set { Parameter.Value = value; }
    }

    public this(p: IDbDataParameter) {
        parameter = p;
    }
}


[HasLogging]
public class DataReader: IDisposable {

    [Accessor] protected reader: IDataReader;

    public this(r: IDataReader) {
        reader = r;
    }

    public Dispose(): void {
        reader.Dispose();
    }

    public enumerate(lambda: void -> void): void {
        while(reader.Read()) {
            lambda();
        }
    }

    public enumerate(lambda: int -> void): int {
        mutable i = 0;
        while(reader.Read()) {
            lambda(i);
            i++;
        }
        i;
    }

    public getValue[T](field: string, default: T): T {
        def ordinal = reader.GetOrdinal(field);
        getValue(ordinal, default);
    }

    public getValue[T](field: int, default: T): T {
        if(reader.IsDBNull(field)) {
            default;
        } else {
            unchecked reader.GetValue(field) :> T;
        }
    }

    public getValue[T](field: string): T {
        def ordinal = reader.GetOrdinal(field);
        getValue(ordinal);
    }

    public getValue[T](field: int): T {
        if(reader.IsDBNull(field)) {
            default(T);
        } else {
            unchecked reader.GetValue(field) :> T;
        }
    }
}


} // namespace httplib
