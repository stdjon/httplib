using System;
using System.Collections.Generic;
using System.Collections.Specialized;
using System.IO;
using System.IO.Compression;
using System.Net;
using System.Text.RegularExpressions;
using System.Text.Encoding;

using Nemerle.Utility;


namespace httplib {

//------------------------------------------------------------------------------

public type PageData = Dictionary[string, object];
public type PageDataArray = array[PageData];
public type PartialData = Dictionary[string, string];


//------------------------------------------------------------------------------
// HTTP (method)

// Defined in order used in http://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html
type Method = HTTP;
public enum HTTP {
    | INVALID_

    | OPTIONS
    | GET
    | HEAD
    | POST
    | PUT
    | DELETE
    | TRACE
    | CONNECT

    | COUNT_
}


public module MethodExtensions {
    public methodType(this name: string): Method {
        match(name.ToUpper()) {
            | "OPTIONS" => OPTIONS
            | "GET" => GET
            | "HEAD" => HEAD
            | "POST" => POST
            | "PUT" => PUT
            | "DELETE" => DELETE
            | "TRACE" => TRACE
            | "CONNECT" => CONNECT
            | _ => INVALID_
        }
    }

    public methodName(this mth: Method): string {
        match(mth) {
            | OPTIONS => "OPTIONS"
            | GET => "GET"
            | HEAD => "HEAD"
            | POST => "POST"
            | PUT => "PUT"
            | DELETE => "DELETE"
            | TRACE => "TRACE"
            | CONNECT => "CONNECT"
            | _ => "?"
        }
    }
}


//------------------------------------------------------------------------------
// Cookie Flags

type CookieFlags = COOKIE;
[Flags]
public enum COOKIE {
    | NONE = 0x00
    | SECURE = 0x01
    | HTTP_ONLY = 0x02

    | SECURE_HTTP_ONLY = 0x03
}


public module CookieFlagExtensions {

    public isSecure(this cookie: CookieFlags): bool {
        (cookie & COOKIE.SECURE) != COOKIE.NONE;
    }

    public isHttpOnly(this cookie: CookieFlags): bool {
        (cookie & COOKIE.HTTP_ONLY) != COOKIE.NONE;
    }
}


//------------------------------------------------------------------------------
// IAssociation
// NB: we may want to vary on the association value type as well (for cookies).

public interface IAssociation[T] {
    public type SystemType = T;

    Item[key: string]: string { get; }
    Keys: array[string] { get; }
    forEach(lambda: string * string -> void): void;
    toSystemType(): T;
}


//------------------------------------------------------------------------------
// Array

public class Array {

    type SystemType = array[string];

    arr: SystemType;

    public this(data: SystemType) {
        arr = data;
    }

    public Item[index: int]: string {
        get { arr[index]; }
    }

    public Length: int {
        get { arr.Length; }
    }

    public forEach(lambda: string -> void): void {
        foreach(v in arr) {
            lambda(v);
        }
    }

    public toSystemType(): SystemType {
        arr;
    }
}


//------------------------------------------------------------------------------
// HashTable

public class HashTable: IAssociation[NameValueCollection] {

    type SystemType = NameValueCollection;

    nvc: SystemType;

    public this() {
        nvc = SystemType();
    }

    public this(data: SystemType) {
        nvc = data;
    }

    public Item[key: string]: string {
        get { nvc[key]; }
        set { nvc[key] = value; }
    }

    public Keys: array[string] {
        get { nvc.AllKeys; }
    }

    public forEach(lambda: string * string -> void): void {
        foreach(k in Keys) {
            lambda(k, Item[k]);
        }
    }

    public toSystemType(): SystemType {
        nvc;
    }
}


//------------------------------------------------------------------------------
// CookieTable

public class CookieTable: IAssociation[CookieCollection] {

    type SystemType = CookieCollection;

    cc: SystemType;
    // As long as Item[key].set is unimplemented (i.e. cc can't change), we can
    // cache the cookie keys...
    mutable keys: array[string];

    public this() {
        cc = SystemType();
        keys = null;
    }

    public this(data: SystemType) {
        cc = data;
    }

    public Item[key: string]: string {
        get {
            mutable v = null;
            // No CookieCollection.TryGetValue() method...
            try { v = cc[key].Value; } catch { | _ => {}}

            v ? v.ToString() : null;
        }
//        set { cc[key] = value; }
    }

    public Keys: array[string] {
        get { cachedKeys(); }
    }

    public forEach(lambda: string * string -> void): void {
        foreach(k in Keys) {
            lambda(k, Item[k]);
        }
    }

    public toSystemType(): SystemType {
        cc;
    }

    cachedKeys(): array[string] {
        when(keys == null) {
            def k = cc.Count;
            mutable result: array[string];

            Array.Resize(ref result, k);
            for(mutable i = 0; i < k; i++) {
                result[i] = cc[i].Name;
            }
            keys = result;
        }
        keys;
    }
}


//------------------------------------------------------------------------------
// CookieSetter

public class CookieSetter {

    response: HttpListenerResponse;

    public this(r: HttpListenerResponse) {
        response = r;
    }

    public add(name: string, value: string, flags = COOKIE.NONE): void {

        add(name, value, null, null, flags);
    }

    public add(name: string,
        value: string, domain: string, flags = COOKIE.NONE): void {

        add(name, value, domain, null, flags);
    }

    public add(name: string,
        value: string, expiry: DateTime?, flags = COOKIE.NONE): void {

        add(name, value, null, expiry, flags);
    }

    // NB: Secure/HttpOnly flags set on a System.Net.Cookie do not seem to get
    //   written out into the response header - would be nice to find out why.
    //   (For now, we can write out the Set-Cookie header in full, which works
    //   acceptably.)
    public add(name: string, value: string,
        domainp: string, expiry: DateTime?, flags = COOKIE.NONE): void {

        def (domain, path) = splitPathFromDomain(domainp);

        def domain_spec =
            !String.IsNullOrEmpty(domain) ? $"; Domain=$domain" : "";

        def expiry_fmt = expiry ?
            (expiry :> DateTime).ToUniversalTime().ToString("r") : null;
        def expiry_spec = expiry_fmt ? $"; Expires=$expiry_fmt": "";

        def secure = flags.isSecure() ? "; Secure" : "";
        def http_only = flags.isHttpOnly() ? "; HttpOnly" : "";

        // From my testing, this method WebHeaderCollection.Add(string) is the
        // _only_ reliable way to set multiple cookies with different flags in
        // the same Response.
        response.Headers.Add($"Set-Cookie: $name=$value; "
            $"Path=$path$domain_spec$expiry_spec$secure$http_only");
    }

    public expire(name: string, flags = COOKIE.NONE): void {
        expire(name, null, flags);
    }

    public expire(name: string,
        domain: string, flags = COOKIE.NONE): void {

        add(name, null, domain, Date.Now.AddMinutes(-10), flags);
    }

    // Path value for cookies can be specified by appending to the end of the
    // domain parameter e.g. "domain.com/path". If no / is present, the path
    // value defaults to "/" (which seems to be the most common path value in
    // actual real-word usage...)
    static splitPathFromDomain(mutable domain: string): string * string {
        mutable path = "/";
        def slash_index =
            !String.IsNullOrEmpty(domain) ? domain.IndexOf('/') : -1;

        when(slash_index >= 0) {
            path = domain.Substring(slash_index);
            domain = domain.Substring(0, slash_index);
        }
        (domain, path)
    }
}


//------------------------------------------------------------------------------
// Context

public class Context {
    [Accessor(flags = WantSetter)] mutable file_stream: FileStream;
    [Accessor(flags = WantSetter)] mutable searched_for_file_stream: bool;
    [Accessor(flags = WantSetter)] mutable page_data: PageData;
    [Accessor(flags = WantSetter)] mutable vars: HashTable;
    [Accessor(flags = WantSetter)] mutable headers: Headers;

    public this() {
        file_stream = null;
        searched_for_file_stream = false;
        vars = HashTable();
    }

    internal complete(): void {
        def fs = FileStream;
        when(fs != null) {
            fs.Close();
        }
    }
}


//------------------------------------------------------------------------------
// Request

public class Request {

    [Accessor] http_method: Method;
    [Accessor] url: Uri;
    [Accessor] raw_url: string;
    [Accessor] referrer: Uri;
    [Accessor] local_endpoint: IPEndPoint;
    [Accessor] remote_endpoint: IPEndPoint;
    [Accessor] query_string: HashTable;
    [Accessor] headers: HashTable;
    [Accessor] cookies: CookieTable;
    [Accessor] path: Array;
    [Accessor] accept: Array;
    [Accessor] raw_path: string;
    [Accessor] user_agent: string;
    [Accessor] is_authenticated: bool;
    [Accessor] is_secure: bool;
    [Accessor] is_web_socket: bool;
    [Accessor] http_version: int;
    // TODO: is it possible to refactor these setters/mutables away?
    [Accessor(flags = WantSetter)] mutable extension: string;

    // Convenience wrapper for headers
    public Item[key: string]: string {
        get { Headers[key] }
    }

    public this(req: HttpListenerRequest, force_insecure: bool = false) {

        http_method = req.HttpMethod.methodType();
        url = req.Url;
        raw_url = req.RawUrl;
        referrer = req.UrlReferrer;
        local_endpoint = req.LocalEndPoint;
        remote_endpoint = req.RemoteEndPoint;
        query_string = HashTable(req.QueryString);
        headers = HashTable(req.Headers);
        cookies = CookieTable(req.Cookies);
        path = makePathArray(RawUrl);
        accept = Array(req.AcceptTypes);
        raw_path = makeRawPath(Path);
        user_agent = req.UserAgent;
        is_authenticated = req.IsAuthenticated;
        is_secure = req.IsSecureConnection || force_insecure;
        is_web_socket = req.IsWebSocketRequest;
        http_version = makeHttpVersion(req.ProtocolVersion);
        extension = makeExtension(Path);
    }

    /** convert a RawUrl -> path component Array
     *  "/a/b//c" -> [ "a", "b", "c" ] */
    makePathArray(raw_url: string): Array {

        def path_only = raw_url.Split(array['?'], StringSplitOptions.None);
        def path_section =
            (path_only.Length > 0) ? path_only[0] : raw_url;

        def path_split = path_section.Split(
            array['/'], StringSplitOptions.RemoveEmptyEntries );

        Array(path_split);
    }

    makeExtension(path: Array, full: bool = false): string {

        def last_path =
            (path.Length > 0) ? path[path.Length - 1] : "";

        def last_split = last_path.Split(
            array['.'], StringSplitOptions.None );

        if(last_split.Length > 1) {
            if(full) {
                def len = last_split.Length - 1;
                mutable tmp: array[string] = array(len);
                Array.Copy(last_split, 1, tmp, 0, len);
                String.Join(".", tmp)
            } else {
                last_split[last_split.Length - 1];
            }
        } else {
            ""
        }
    }

    //TODO: "/" + String.Join("/", ...)
    makeRawPath(path: Array): string {
        String.Join("/", path.toSystemType());
    }

    makeHttpVersion(version: Version): int {
        (version.Major) + 10 + (version.Minor);
    }
}


//------------------------------------------------------------------------------
// Response

public class Response {

    public ContentLength: long {
        get { response.ContentLength64 }
        set { response.ContentLength64 = value; }
    }

    public ContentType: string {
        get { response.ContentType }
        set { response.ContentType = value; }
    }

    public StatusCode: int {
        get { response.StatusCode }
        set { response.StatusCode = value; }
    }

    public StatusDescription: string {
        get { response.StatusDescription }
        set { response.StatusDescription = value; }
    }

    // Convenience wrapper around (StatusCode, StatusDescription)
    public Status: int * string {
        get { (StatusCode, StatusDescription) }
        set { (StatusCode, StatusDescription) = value; }
    }

    // Convenience wrapper for headers
    public Item[key: string]: string {
        get { response.Headers[key] }
        // TODO: more extensive testing whether the below is actually better than
        //   response.Headers[key] = value in all situations...
        set { response.Headers.Add($"$key: $value") }
    }

    public Cookies: CookieSetter { get; private set; }

    mutable response: HttpListenerResponse;
    mutable output: array[byte];

    public this(res: HttpListenerResponse, output_length = 1024) {

        Cookies = CookieSetter(res);
        response = res;
        output = array(output_length);
    }

    public append(params data: array[string]): void {
        foreach(d in data) {
            def buffer = UTF8.GetBytes(d);
            writeToOutput(buffer);
        }
    }

    public appendBytes(data: array[byte]): void {
        writeToOutput(data);
    }

    writeToOutput(data: array[byte]): void {
        def new_length = ContentLength + data.Length;

        when(output.Length < new_length) {
            def size = output.Length * 3 / 2;
            def resize = (size > new_length) ? size : new_length :> int;
            Array.Resize(ref output, resize);
        }

        data.CopyTo(output, ContentLength);
        ContentLength = new_length;
    }

    internal complete(): void {
        def os = response.OutputStream;
        os.Write(output, 0, ContentLength :> int);
        os.Close();
    }

    internal encode(encoding: string): void {
        when(encoding != null) {
            Array.Resize(ref output, ContentLength :> int);
            using(ms = MemoryStream()) {
                using(str = match(encoding) {
                    | "gzip" => GZipStream(ms, CompressionMode.Compress)
                    | "deflate" => DeflateStream(ms, CompressionMode.Compress)
                    | _ => null }) {

                    str.Write(output, 0, output.Length);
                }
                output = ms.ToArray();
                ContentLength = output.Length;
            }
            this["Content-Encoding"] = encoding;
        }
    }
}


//------------------------------------------------------------------------------
// Route

public class Route {

    [Accessor] pattern: string;
    regex: Regex;
    mutable captures: array[string] = array(0);

    public this(path: string) {

        mutable rx = path;

        // {{name}}  -->  ([A-Za-z0-9.-]+)
        process(ref rx, @"{{(\w+)}}", _ => { @"([A-Za-z0-9_.-]+)" });

        // {{name:pattern}}  -->  (pattern)
        process(ref rx, @"{{(\w+):([^}]+)}}", p => { "(" + p + ")" });

        //anchor start/end of regex
        rx = "^" + rx + "$";

        regex = Regex(rx);
        pattern = rx;
    }

    public doMatch(path: string, vars_ref: ref HashTable): bool {
        def result = regex.IsMatch(path);
        mutable vars = vars_ref; // cannot store ref/out parameters in closures

        when(result && vars != null) {
            _ = regex.Replace(path, m => {

                for(mutable i = 1; i < m.Groups.Count; i++) {
                    def k = captures[i - 1];
                    def v = m.Groups[i].ToString();
                    vars[k] = Uri.UnescapeDataString(v);
                }
                ""
            }, 100, 0);
        }
        vars_ref = vars;
        result;
    }

    process(input: ref string, pattern: string, replace: string -> string): void {
        input = Regex.Replace(input, pattern, m => {

            def v: string = m.Groups[1].ToString();
            def patt: string = m.Groups[2].ToString();
            def e = captures.Length;

            Array.Resize(ref captures, e + 1);
            captures[e] = v;

            replace(patt);
        });
    }
}


//------------------------------------------------------------------------------
//

public module Date {

    public FromTicks(ticks: long): DateTime {
        DateTime(ticks, DateTimeKind.Utc);
    }

    public ToTicks(dt: DateTime): long {
        dt.ToUniversalTime().Ticks;
    }

    public Now: DateTime {
        get { DateTime.UtcNow; }
    }

    public NowTicks: long {
        get { DateTime.UtcNow.Ticks; }
    }
};


//------------------------------------------------------------------------------
// Extension Methods

public module ExceptionExtensions {

    /** In DEBUG builds, try to extract ]ocation information for the exception's
     *  strack trace. If all of (file, line, column) are available, use a standard
     *  "file:line:column:" format so that tools (e.g. Vim, Visual Studio, etc)
     *  can parse the stacktrace log and jump to the code location (for a better
     *  runtime debugging experience). Otherwise display only "file:" or
     *  "<unknown>:" as the location.
     *  This feature relies on having PDB files (generated by ncc's -g flag), so
     *  in a non-debug build, just use the default stack trace information in the
     *  exception. */
    public GetExtendedStackTrace(this ex: Exception): string {
#if DEBUG
        def st = Diagnostics.StackTrace(ex, true);
        def frames = st.GetFrames();
        mutable stacktrace = "";

        foreach(f in frames) {
            def file = f.GetFileName();
            def method = f.GetMethod();

            def location = String.IsNullOrEmpty(file) ? "<unknown>" : {
                def line = f.GetFileLineNumber();
                def column = f.GetFileColumnNumber();

                match(line, column) {
                    | (0, 0) => $"$file"
                    | _ => $"$file:$line:$column"
                }
            }

            stacktrace += $"  $location: $method\n"
        }
        stacktrace;
#else
        ex.StackTrace;
#endif
    }
}

} // namespace httplib
