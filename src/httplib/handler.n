using System;
using System.IO;

using Nemerle.IO;


namespace httplib {

//------------------------------------------------------------------------------
// HandlerData

/** This exists basically to make the Handler ctor (and subclasses) simpler and
 *  easier to write. */
[Record]
public class HandlerData {
    public Context: Context { get; set; }
    public Request: Request { get; set; }
    public Response: Response { get; set; }
    public Server: Server { get; set; }
}


//------------------------------------------------------------------------------
// Handler

public class Handler {

    public virtual Context: Context { get; set; }
    public virtual Request: Request { get; set; }
    public virtual Response: Response { get; set; }
    public virtual Server: Server { get; set; }

    public this(hd: HandlerData) {
        Context = hd.Context;
        Request = hd.Request;
        Response = hd.Response;
        Server = hd.Server;
    }

    public virtual callback(): void {
        // do stuff with Context/Request/Response
    }
}


//------------------------------------------------------------------------------
// CallbackHandler

public class CallbackHandler: Handler {

    lambda: Server.Callback;

    public this(hd: HandlerData, cb: Server.Callback) {
        base(hd);
        lambda = cb;
    }

    public override callback(): void {
        lambda(Context, Request, Response);
    }
}


public class CallbackHandlerWrapper {

    lambda: Server.Callback;

    public this(cb: Server.Callback) {
        lambda = cb;
    }

    public unwrap(hd: HandlerData): Handler {
        CallbackHandler(hd, lambda);
    }
}


//------------------------------------------------------------------------------
// ComplexHandler

public class ComplexHandler: Handler {

    public this(hd: HandlerData) {
        base(hd);
    }

    public override callback(): void {
        initialize();

        def path = setPagePath(Request.RawPath);
        mutable fs = null;
        when(!String.IsNullOrEmpty(path)) {
            fs = openFile(path);
        }

        mutable data = null;
        when(fs != null) {
            data = setPageData();
        }

        def ext = Path.GetExtension(path).Substring(1);
        mutable renderer = getRenderer(ext);
        when(renderer != null) {
            def page = renderer.loadPage(fs);
            def text = page.render(data);
            Response.append(text);
            Response.ContentType = setContentType();
            Response.Status = setStatus();
        }

        finish();
    }

    public virtual initialize(): void {}

    public virtual setPagePath(_path: string): string {
        Request.RawPath;
    }

    public virtual openFile(path: string): Stream {
        Server.openLocalFile(path);
    }

    public virtual setContentType(): string {
        "text/plain"
    }

    public virtual setPageData(): PageData {
        null
    }

    public virtual getRenderer(ext: string): Renderer {
        Server.Renderers[ext];
    }

    public virtual setStatus(): int * string {
        (200, "OK");
    }

    public virtual loadContent(): void {}

    public virtual finish(): void {}
};


} // namespace httplib
