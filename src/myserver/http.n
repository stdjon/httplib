using System;
using System.Collections.Generic;
using System.IO;
using System.Threading;

using Nemerle.Extensions;
using Nemerle.IO;
using Nemerle.Utility;

using httplib;


//------------------------------------------------------------------------------
// MyServerConfiguration

[Record]
class MyServerConfiguration: ServerConfiguration {
    public this() {}
}

//------------------------------------------------------------------------------
// MyHandler

class MyHandler: Handler {

    public this(
        context: Context, request: Request, response: Response, server: Server) {

        base(context, request, response, server);
    }

    public override callback(): void {
        def id = Context.Vars["id"];
        def rest = Context.Vars["rest"];

        Response.append($"<html><body><h1>Thread (ID=$id)</h1>");
        Response.append($"<p><tt>[Rest = $rest]</tt></p></body></html>");
        Response.Status = (200, "OK");
    }
};


class MyComplexHandler: ComplexHandler {

    public this(
        context: Context, request: Request, response: Response, server: Server) {

        base(context, request, response, server);
    }

    public override setPagePath(_path: string): string {
        "magic.nyml";
    }

    public override setContentType(): string {
        "text/html"
    }

    public override setPageData(): PageData {
        def nustache_table = PageData() <- [
            "RawPath" = Request.RawPath,
            "Path" = Request.Path.toSystemType(),
        ];

        mutable vars: array[string] = array(0);
        mutable qs: array[string] = array(0);
        mutable hdrs: array[string] = array(0);
        mutable i = 0;
        Context.Vars.forEach((k, v) => {
            System.Array.Resize(ref vars, i + 1);
            vars[i] = ($"$k = $v").ToString();
            i++;
        });
        nustache_table.Add("Vars", vars);

        i = 0;
        Request.QueryString.forEach((k, v) => {
            System.Array.Resize(ref qs, i + 1);
            qs[i] = ($"$k = $v").ToString();
            i++;
        });
        nustache_table.Add("QueryString", qs);

        i = 0;
        Request.Headers.forEach((k, v) => {
            System.Array.Resize(ref hdrs, i + 1);
            hdrs[i] = ($"$k = $v").ToString();
            i++;
        });
        nustache_table.Add("Headers", hdrs);

        when(Context.Vars["name"] != null) {
            nustache_table.Add("name", Context.Vars["name"]);
        }
        nustache_table;
    }

    public override finish(): void {
        print("HERE HERE HERE...\n");
        Response.setCookie("CookieTestName", "CookieTestValue");
    }
};


class MyTestHandler: MyComplexHandler {
    public this(
        context: Context, request: Request, response: Response, server: Server) {

        base(context, request, response, server);
    }

    public override setPagePath(_path: string): string {
        "test1.nyml";
    }
};


//------------------------------------------------------------------------------
// MyServer

class MyServer: Server {

    public new Configuration: MyServerConfiguration {
        get { base.Configuration :> MyServerConfiguration }
    }

    public this(path: string) {
        def config = loadConfiguration.[MyServerConfiguration](path);
        base(config);
    }

    public this(config: MyServerConfiguration) {
        base(config);
    }

    protected override notFoundRespose(
        _context: Context, request: Request, response: Response): void {
        def data = PageData() <- [
            "PATH" = request.RawUrl.ToString(),
        ];
        def output = renderPage("404.nyml", data);
        response.append(output);
        response.Status = (404, "Not Found");
    }

    // overridden 500 error response...
    protected override internalServerErrorResponse(
        _context: Context, request: Request, response: Response): void {

        def data = PageData() <- [
            "PATH" = request.RawUrl.ToString(),
        ];
        def output = renderPage("500.nyml", data);
        response.append(output);
        response.Status = (500, "Internal Server Error");
    }

    protected override internalServerError(ex: Exception): void {
        print("INTERNAL SERVER ERROR!\n"
            $"$(ex.Message)\n"
            $"$(ex.StackTrace)\n");
    }
}


module Test {
    Main(_args: array[string]): void {

        mutable running = true;
        def config_path = "src/myserver/config.yml";
        print("ServerConfiguration: $config_path\n");

        using(mutable my_server = MyServer(config_path)) {

            my_server.route([
                (HTTP.GET, "/th/{{id:[0-9]+}}/{{rest:.*}}"),
                (HTTP.GET, "/th/{{id:[0-9]+}}")], MyHandler);

            my_server.route([
                (HTTP.GET, "/a/{{b}}/{{c}}"),
                (HTTP.GET, "/order/{{number:[0-9]+}}"),
                (HTTP.POST, "/form{{_check}}")], MyComplexHandler);

            my_server.route(HTTP.GET, "/post/{{name}}", MyTestHandler);

            // redirection test...
            my_server.route(HTTP.GET, "/redir{{aim:.*}}", (ctx, _req, res) => {
                res["Location"] = ctx.Vars["aim"];
                res.Status = (302, "Moved Permanently");
            });

            my_server.route(HTTP.GET, "/", (_ctx, _req, res) => {
                res.append(my_server.renderPage("index.yml"));
                res.Status = (200, "OK");
            });

            my_server.context(HTTP.GET, ".*", PageData() <- [ "MAGIC" = 1 ]);

            while(running) {
                my_server.update();
                Thread.Sleep(1000);
            }
        }
    }
}


