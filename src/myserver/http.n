using System;
using System.Collections.Generic;
using System.IO;
using System.Net;
using System.Text;
using System.Text.RegularExpressions;
using System.Threading;

using Nemerle.Extensions;
using Nemerle.Utility;

using httplib;


//------------------------------------------------------------------------------
// MyServerConfiguration

[YamlRecord]
class MyServerConfiguration: ServerConfiguration {
}


//------------------------------------------------------------------------------
// MyHandler

class MyHandler: Handler {

    public this(hd: HandlerData) {
        base(hd);
    }

    public override callback(): void {
        def id = Context.Vars["id"];
        def rest = Context.Vars["rest"];

        Response.append($"<html><body><h1>Thread (ID=$id)</h1>");
        Response.append($"<p><tt>[Rest = $rest]</tt></p></body></html>");
        Response.Status = (200, "OK");
    }
}


class MyComplexHandler: ComplexHandler {
    page_data: PageData;

    public this(hd: HandlerData) {
        base(hd);
        page_data = getPageData();
    }

    public override PagePath: string {
        get { "magic.nyml" }
    }

    public override ContentType: string {
        get { "text/html" }
    }

    public override PageData: PageData {
        get { page_data }
    }

    getPageData(): PageData {
        def page_data = PageData() <- [
            "RawPath" = Request.RawPath,
            "Path" = Request.Path.toSystemType(),
        ];

        mutable vars: array[string] = array(0);
        mutable qs: array[string] = array(0);
        mutable hdrs: array[string] = array(0);
        mutable i = 0;
        Context.Vars.forEach((k, v) => {
            System.Array.Resize(ref vars, i + 1);
            vars[i] = ($"$k = $v").ToString();
            i++;
        });
        page_data.Add("Vars", vars);

        i = 0;
        Request.QueryString.forEach((k, v) => {
            System.Array.Resize(ref qs, i + 1);
            qs[i] = ($"$k = $v").ToString();
            i++;
        });
        page_data.Add("QueryString", qs);

        i = 0;
        Request.Headers.forEach((k, v) => {
            System.Array.Resize(ref hdrs, i + 1);
            hdrs[i] = ($"$k = $v").ToString();
            i++;
        });
        page_data.Add("Headers", hdrs);

        when(Context.Vars["name"] != null) {
            page_data.Add("name", Context.Vars["name"]);
        }
        page_data;
    }

    public override finish(): void {
        Response.Cookies.add("CookieTestName", "CookieTestValue");
    }
}


class MyTestHandler: MyComplexHandler {

    public this(hd: HandlerData) {
        base(hd);
    }

    public override PagePath: string {
        get { "test1.nyml" }
    }
}



class MyDatabaseHandler: ComplexHandler {

    public this(hd: HandlerData) {
        base(hd);
    }

    public override PagePath: string {
        get { "books.nyml" }
    }

    public override ContentType: string {
        get { "text/html" }
    }

    public override PageData: PageData {
        get { readDb() }
    }

    readDb(): PageData {
        PageData() <- [
            "Description" = "Here are some authors and books...",
            "Authors" = getDbArray("Name", "Authors"),
            "Books" = getDbArray("Title", "Books"),
        ];
    }

    getDbArray(column: string, table: string): array[string] {
        mutable result = array(0);
        def c = match(column) {
            | "Name" => "Name"
            | "Title" => "Title"
            | _ => "#"
        };
        def t = match(table) {
            | "Authors" => "Authors"
            | "Books" => "Books"
            | _ => "#"
        };
        def command = Server.Database.createCommand($"SELECT $c FROM $t");
        using(reader = command.executeReader()) {
            _ = reader.enumerate(j => {
                Array.Resize(ref result, j + 1);
                result[j] = reader.getValue(c);
            });
        }
        result;
    }
}


//------------------------------------------------------------------------------
// MyTestModule

public interface IMyTestModule: IModuleInterface {

    Server: Server { get }; // Module.Server 'magically' fulfils this API
    testMethod(): string;
}


public class MyTestModule: Module, IMyTestModule {

    text: string;

    // overridden from Module, indicates the module interface we're providing...
    public override InterfaceType: Type {
        get { typeof(IMyTestModule) }
    }

    public this(data: ModuleData) {

        text = if(data != null && data.ContainsKey("Text")) {
            $<#<p>$(data["Text"])</p>#>;
        } else {
            "<p>This is some test text which the module returned.</p>";
        }
    }

    public testMethod(): string {
        text;
    }
}


//------------------------------------------------------------------------------
// MyServer

class MyServer: Server {

    [Accessor(flags = WantSetter)] mutable edit_text: string = "";
    [Accessor(flags = WantSetter)] mutable encoded_text: string = "";
    [Accessor(flags = WantSetter)] mutable edit_mode: string = "bbcode";

    public new Configuration: MyServerConfiguration {
        get { base.Configuration :> MyServerConfiguration }
    }

    public this(path: string) {
        def config = loadConfiguration.[MyServerConfiguration](path);
        this(config);
    }

    public this(config: MyServerConfiguration) {
        base(config);
    }

    public override notFoundRespose(
        _context: Context, request: Request, response: Response): void {
        def data = PageData() <- [
            "PATH" = request.RawUrl.ToString(),
        ];
        def output = renderPage("404.nyml", data);
        response.append(output);
        response.Status = (404, "Not Found");
    }

    // overridden 500 error response...
    public override internalServerErrorResponse(
        _context: Context, request: Request, response: Response): void {

        def data = PageData() <- [
            "PATH" = request.RawUrl.ToString(),
        ];
        def output = renderPage("500.nyml", data);
        response.append(output);
        response.Status = (500, "Internal Server Error");
    }

    public pwResponse(
        context: Context, request: Request, response: Response): void {

        def auth = getModuleInterface.[IAuth]("Pbkdf2Auth");
        if(auth != null) {
            def test_pw = "n0ts3cr3t";
            def record = auth.createPasswordRecord(test_pw);
            mutable sw = Diagnostics.Stopwatch();
            sw.Start();
            def valid = auth.validatePassword(test_pw, record);
            sw.Stop();
            def tvalid = sw.ElapsedMilliseconds;
            sw.Reset();
            sw.Start();
            def invalid = auth.validatePassword("owls", record);
            sw.Stop();
            def tinvalid = sw.ElapsedMilliseconds;
            sw.Reset();
            response.append("<html><body><h1>Auth test</h1>"
                $"<p>Input password is: '$test_pw'.</p>"
                $"<p>Password record is: '$record'.</p>"
                $"<p>Password record length is: '$(record.Length)'.</p>"
                $"<p>Validation result is: '$valid'.</p>"
                $"<p>Validation took: $(tvalid)ms.</p>"
                $"<p>Failed result is: '$invalid'.</p>"
                $"<p>Failed took: $(tinvalid)ms.</p>"
                "</body></html>");
            response.Status = (200, "OK");
        } else {
            internalServerErrorResponse(context, request, response);
            Log.w("IPbkdf2Auth module was not found.");
        }
    }

    public testCorsGetResponse(
        _context: Context, request: Request, response: Response): void {

        def origin = request["Origin"];
        def data = PageData() <- [ "GetOrigin" = origin ];

        Log.i("GetOrigin: {0}", origin);
        response.append(renderPage("cors.nyml", data));
        response.Cookies.add("CorsTestInsecureCookie", "OK");
        response.Status = (200, "OK");
    }

    public testCorsPostResponse(
        _context: Context, request: Request, response: Response): void {

        def origin = request["Origin"];

        Log.i("PostOrigin: {0}", origin);
        if(request.IsSecure) {
            if(!String.IsNullOrEmpty(origin)) {
                response.append($"<h3>Post Origin: $(origin)</h3>");
                response["Access-Control-Allow-Origin"] = origin;
                response["Access-Control-Allow-Credentials"] = "true";
                response.Cookies.add("CorsTestSecureCookie", "OK", COOKIE.SECURE);
                response.Status = (200, "OK");
            } else {
                response.Status = (500, "CORS Error");
            }
        } else {
            response.Status = (403, "Forbidden");
        }
    }

    public ajaxResponse(
        _context: Context, request: Request, response: Response): void {

        def bg = request.QueryString["bg"];
        def is_main_call = (HTTP.GET == request.HttpMethod);
        def k = is_main_call ? "MainCall" : "AjaxCall";
        mutable data = PageData();

        data[k] = 1;

        when(bg != null) {
            data["BgCol"] = bg;
        }


        def output = ((is_main_call) ?
            renderPage(_, _) : renderPageContent(_, _))("ajax.nyml", data);

        response.append(output);
        response.Status = (200, "OK");
    }


    public editTextResponse(
        context: Context, _request: Request, response: Response): void {

        mutable sync = false;
        def m = context.Vars["m"];
        if(m != null) {
            match(m) {
                | "bbcode" | "textile" | "htmlsan" => edit_mode = m
                | _ => {}
            }
        } else {

            def v = context.Vars["v"];

            EditText =
                if(v != null) {
                    sync = true;
                    v
                } else {
                    def a = Math.Max(0, Int32.Parse(context.Vars["a"]));
                    def b = Math.Min(Int32.Parse(context.Vars["b"]), EditText.Length);
                    def s = context.Vars["s"];

                    match(a, b, s) {
                        | (0, 0, "") => ""
                        | _ => {
                            EditText.Substring(0, a) + s + EditText.Substring(b)
                        }
                    }
                };
        }

        def bbcode = getModuleInterface.[ITextTransform]("BbCode");
        def textile = getModuleInterface.[ITextTransform]("Textile");
        def htmlsan = getModuleInterface.[ITextTransform]("HtmlSan");

        def encoded = match(edit_mode) {
            | "bbcode" => bbcode.transform(EditText)
            | "textile" => textile.transform(EditText)
            | "htmlsan" => htmlsan.transform(EditText)
            // even though this is a silly toy example, it's still vulnerable
            // to <script> attacks and the like.
            | _ => WebUtility.HtmlEncode(EditText)
        };

        def encode_json = sync ?
            syncText(encoded) :
            diffText(EncodedText, encoded);
        EncodedText = encoded;

        response.append(encode_json);
        response.Status = (200, "OK");
    }

    // returns the difference between o and n as a JSON string
    // TODO: this is a naive port of the client side code, a more idiomatic
    //   Nemerle implementation would be nice.
    diffText(o: string, n: string): string {

        def clamp(s: string, i: int): int {
            def r = Math.Max(0, Math.Min(i, s.Length));
            r;
        }

        def index(s: string, i: int): char {
            mutable result = '\0';
            try { result = s[clamp(s, i)] } catch { | _ => {} }
            result;
        }

        def slice(s: string, a: int, b: int): string {
            mutable result = "";
            def start = clamp(s, a);
            def len = clamp(s, b - a);
            try { result = s.Substring(start, len); } catch { _ => {} }
            result;
        }

        def ol = o.Length;
        def nl = n.Length;
        def ml = Math.Min(ol, nl);
        mutable i = 0;
        mutable a, b, s;
        while(i < ml && index(o, i) == index(n, i)) {
            i++;
        }
        a = i;
        if(nl == ol) {
            mutable j = i;
            while(j < ml && index(o, j) != index(n, j)) {
                j++;
            }
            b = j;
            s = slice(n, i, j);
        } else if(nl > ol) {
            mutable j = (nl - 1), k = (ol - 1);
            while(j > i && k > i && index(o, k) == index(n, j)) {
                j--;
                k--;
            }
            b = k+1;
            s = slice(n, i, j+1);

        } else { // nl < ol
            mutable j = nl, k = ol;
            while(j > i && k > i && index(o, k) == index(n,j)) {
                j--;
                k--;
            }
            b = k;
            when(index(o, k) != index(n,j)) {
                b++;
            }
            s = slice(n, i, (b-i));
        }
        //Log.t("diffText("
        //    "\"{0}\", \"{1}\") -> a={2}, b={3}, s=\"{4}\"", o, n, a, b, s);
        (JsonObject() <- [
            "a" = a,
            "b" = b,
            "s" = s,
        ]).toString();
    }

    syncText(s: string): string {
        (JsonObject() <- [ "v" = s ]).toString();
    }

    public uploadImageResponse(
        context: Context, _request: Request, response: Response): void {

        def transformImage(memstr, xform) {
            def iimagefactory =
                getModuleInterface.[IImageTransformFactory]("ImageProcessor");
            def iimage = iimagefactory.makeImageTransform();
            _ = iimage.load(memstr);
            _ = iimage.transform(xform);
            //_ = iimage.save($"./tmp-$xform.png");
            def outstr = MemoryStream(4096);
            _ = iimage.save(outstr);
            def outbuf = outstr.GetBuffer();
            def outb64 = Convert.ToBase64String(outbuf);
            $"data:image/png;base64,$outb64";
        }

        def f = context.Vars["f"];
        mutable json;
        when(!String.IsNullOrEmpty(f)) {
            def b64 = Regex.Match(f, @"data:image/(?<type>.+?),(?<data>.+)").Groups["data"].Value;
            def data = Convert.FromBase64String(b64);
            def memstr = MemoryStream(data);
            def out40 = transformImage(memstr, "avatar40");
            def out80 = transformImage(memstr, "avatar64");
            def out64 = transformImage(memstr, "avatar80");
            def out120 = transformImage(memstr, "avatar120");

            json = JsonObject() <- [
                "i40" = out40,
                "i64" = out64,
                "i80" = out80,
                "i120" = out120,
            ];
        }
        response.append(json.toString());
        response.Status = (200, "OK");
    }

    public checkUserAgentResponse(
        _context: Context, request: Request, response: Response): void {

        response.append($<#<html>$(request["User-Agent"])</html>#>);
        response.Status = (200, "OK");
    }

    public override internalServerError(ex: Exception): void {
        Log.e("INTERNAL SERVER ERROR!\n{0}\n{1}",
            ex.Message, ex.GetExtendedStackTrace());
    }
}


module Test {

    Main(args: array[string]): void {

        def root = (args.Length > 0) ? args[0] : "../../src/myserver/";
        def config_path = (args.Length > 1) ? args[1] : "config.yml";

        Server.CanonicalRoot = root;
        using(mutable my_server = MyServer(config_path)) {

            my_server.route([
                (HTTP.GET, "/th/{{id:[0-9]+}}/{{rest:.*}}"),
                (HTTP.GET, "/th/{{id:[0-9]+}}")], MyHandler);

            my_server.route([
                (HTTP.GET, "/a/{{b}}/{{c}}"),
                (HTTP.GET, "/order/{{number:[0-9]+}}"),
                (HTTP.POST, "/form{{_check}}")], MyComplexHandler);

            my_server.route(HTTP.GET, "/post/{{name}}", MyTestHandler);

            my_server.route(HTTP.GET, "/data", MyDatabaseHandler);

            // redirection test...
            my_server.route(HTTP.GET, "/redir{{location:.*}}", (ctx, _req, res) => {
                res["Location"] = ctx.Vars["location"];
                res.Status = (302, "Moved Permanently");
            });

            my_server.route(HTTP.GET, "/", (_ctx, _req, res) => {
                res.append(my_server.renderPage("index.yml"));
                res.Status = (200, "OK");
            });

            my_server.route(HTTP.GET, "/module", (_ctx, _req, res) => {
                def iface = my_server.getModuleInterface.[IMyTestModule]("MyTestModule");
                def iface2 = my_server.getModuleInterface.[IMyTestModule]("MyTestModule2");
                if(iface != null && iface2 != null) {
                    def same_server = my_server.Equals(iface.Server);
                    res.append("<html><body><h1>Module Test "
                        $"[$(iface.Name) and $(iface2.Name)]</h1>"
                        $"$(iface.testMethod())"
                        $"$(iface2.testMethod())"
                        $"<p>Same server: $same_server</p></body></html>");
                    res.Status = (200, "OK");
                } else {
                    res.append("<html><body><h1>Modules Missing :(</h1></body></html>");
                    res.Status = (500, "Modules missing");
                }
            });

            my_server.route(HTTP.GET, "/pwtest", my_server.pwResponse);

            // This kind of web API (sending tiny string deltas) would be a good
            // use for a WebSocket connection (in due course).
            my_server.route(HTTP.POST, "/edit-do", my_server.editTextResponse);

            my_server.route(HTTP.POST, "/upload-do", my_server.uploadImageResponse);

            my_server.route(HTTP.GET, "/cors", my_server.testCorsGetResponse);
            my_server.route(HTTP.POST, "/cors", my_server.testCorsPostResponse);

            my_server.route(HTTP.GET, "/ajax", my_server.ajaxResponse);
            my_server.route(HTTP.POST, "/ajax", my_server.ajaxResponse);

            my_server.route(HTTP.GET, "/check", my_server.checkUserAgentResponse);

            my_server.route(HTTP.GET, "/mail",  (_ctx, req, res) => {
                def qto = req.QueryString["to"];
                def to = (!String.IsNullOrEmpty(qto)) ? qto : "test@example.com";

                def ismtp = my_server.getModuleInterface.[ISmtp]("Smtp");
                def imail = ismtp.makeEmail();
                imail.From = "test@example.com";
                imail.To = [to];
                imail.Subject = "test mail";
                imail.Body = "this is a test mail...";
                imail.send();
                res.append($"<html><body>Mail sent to $to: check your SMTP server...</body></html>");
                res.Status = (200, "OK");
            });

            my_server.context(HTTP.GET, ".*", PageData() <- [ "MAGIC" = 1 ]);

            my_server.start();


            while(my_server.Running) {
                my_server.update();
                Thread.Sleep(1000);
            }
        }
    }
}


